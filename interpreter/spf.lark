%import common.ESCAPED_STRING
%import common.WS
%import common.CNAME
%ignore WS
%ignore COMMENT

start : program

program : instructions

instructions : instruction+

instruction : instruction_print ";"
            | assignation ";"
            | declaration ";"
            | instruction_ajout ";"
            | instruction_if_else
            | instruction_for_loop
            | instruction_while_loop

declaration : TYPE VARIABLE "=" exp                          -> declare
            | TYPE VARIABLE                                  -> declare

assignation : VARIABLE "=" exp                               -> assign

instruction_print : "afficher" exp ("," exp)*                -> print_exp

instruction_ajout : "ajouter" exp "dans" VARIABLE            -> add_to_list

instruction_if_else : "si" exp "alors" "{" instructions "}" ("sinon" "{" instructions "}")? -> if_else

instruction_for_loop : "pour" "chaque" TYPE VARIABLE "dans" exp "faire" "{" instructions "}"  -> for_loop

instruction_while_loop : "tant" "que" exp "faire" "{" instructions "}"                        -> while_loop

?exp : or_exp

?or_exp : or_exp "ou" and_exp                                -> exp_or
        | and_exp

?and_exp : and_exp "et" not_exp                              -> exp_and
         | not_exp

?not_exp : "non" not_exp                                     -> exp_not
         | comp_exp

?comp_exp : comp_exp COMPARATOR math_exp_add                 -> operation_comparator
          | comp_exp MATH_COMPARATOR math_exp_add            -> operation_math_comparator
          | math_exp_add

?math_exp_add : math_exp_add ADDITIVE math_exp_mul           -> exp_additive
              | math_exp_mul

?math_exp_mul : math_exp_mul MULTIPLICATIVE math_exp_neg     -> exp_multiplicative
              | math_exp_neg

?math_exp_neg : "-" math_exp_neg                             -> exp_neg
              | atom

?atom : value
      | list
      | "(" exp ")"

list : "[" (exp ("," exp)*)? "]"                             -> list_value
     | "[" exp ":" exp "]"                                   -> list_range
     | VARIABLE "[" exp "]"                                  -> list_index
     | "taille" atom                                         -> list_size

?value : STRING                                              -> string_value
       | NUMBER                                              -> number_value
       | BOOLEAN                                             -> boolean_value
       | VARIABLE                                            -> variable_value

ADDITIVE : "+" | "-"
MULTIPLICATIVE : "*" | "/"

COMPARATOR : "==" | "!=" | "vaut" | "ne vaut pas"
MATH_COMPARATOR : "<=" | "<" | ">=" | ">"

STRING.2 : ESCAPED_STRING
NUMBER.2 : /0/ | /[1-9][0-9]*/
BOOLEAN.2 : "vrai" | "faux"

TYPE.2 : "entier" | "liste" | "booléen" | "texte"
VARIABLE : LETTER (LETTER | NUMBER)*
LETTER : /[a-zA-ZàâäéèêëîïôöùûüÿÀÂÄÉÈÊËÎÏÔÖÙÛÜŸ_]/
COMMENT: "#" /[^\n]/*